plugins {
    id 'groovy'
    id 'application'
}

group = 'com.ynab.amazon'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.19'
    implementation 'org.yaml:snakeyaml:2.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    
    // Logging
    implementation 'ch.qos.logback:logback-classic:1.4.7'  // Logback with SLF4J binding
    
    // JavaMail dependencies for email functionality
    implementation 'javax.mail:javax.mail-api:1.6.2'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    
    // Playwright for browser automation
    implementation 'com.microsoft.playwright:playwright:1.40.0'
    
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'cglib:cglib-nodep:3.3.0'    // For mocking classes
    testImplementation 'org.objenesis:objenesis:3.3'  // For mocking classes
}

application {
    mainClass = 'com.ynab.amazon.YNABAmazonTransactionUpdater'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task unitTest(type: Test) {
    dependsOn testClasses
    useJUnitPlatform()
    description = 'Runs only the unit tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/*_UT*'
}

task integrationTest(type: Test) {
    dependsOn testClasses
    dependsOn unitTest
    useJUnitPlatform()
    description = 'Runs only the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/*_IT*'
    testLogging {
        events "passed", "skipped", "failed" // Show output for all tests
        showStandardStreams = true          // Enable standard out/err output
    }
}

test {
    description = 'Runs both unit and integration tests (unit tests first).'
    dependsOn integrationTest
    //finalizedBy integrationTest
    // Optional: Disable default test task execution
    onlyIf { false }
}